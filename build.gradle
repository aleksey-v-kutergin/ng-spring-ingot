group 'ru.rusquant'
version '1.0-SNAPSHOT'

buildscript {
    ext{
        springBootVersion = '1.5.2.RELEASE'
        dmVersion = '1.0.2.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:${dmVersion}"
    }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'io.spring.dependency-management'

sourceCompatibility = 1.8
targetCompatibility = 1.8


repositories {
    mavenCentral()
}


dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-starter-parent:1.5.1.RELEASE'
    }
}

dependencies {
    // Импортирование Servlet API для инициализации и работы нашего
    // приложения в контейнере сервлетов
    providedCompile group: 'javax.servlet', name: 'javax.servlet-api'

    // Библиотека jsp тэков
    compile group: 'javax.servlet', name: 'jstl'

    // Артефакты спринга:
    compile group: 'org.springframework',  name: 'spring-webmvc' // Имплементация MVC
    compile group: 'org.springframework.data', name: 'spring-data-jpa' // Очень удобная штука
    // Интеграция сторонних библиотек в контекст приложения:
    // например кэширование через ecache, отправка электронной почты
    compile group: 'org.springframework', name: 'spring-context-support'

    // Работа с БД и доменный слой
    compile group: 'commons-dbcp', name: 'commons-dbcp' // DB-драйвера
    compile group: 'org.hibernate', name: 'hibernate-core' // Поодержка хибера
    compile group: 'org.hibernate', name: 'hibernate-entitymanager' // Поодержка хибера

    // Логгирование:
    compile group: 'org.slf4j', name: 'jcl-over-slf4j'
    compile group: 'ch.qos.logback', name: 'logback-classic'

    // Процессинг мессаджей: java -> JSON -> java:
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-hibernate5' // Работа с собственными типами хибернейта

    // Тестирование
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

war {
    baseName = 'ng-spring-ingot'
    version = '1.0.0'
    webAppDirName = 'src/main/webapp'
}